plugins {
    id("com.android.application")
}

// count tags and use as Android versionCode
// https://developer.android.com/studio/publish/versioning
def getVersionCode = { ->
    def stream = providers.exec {
        commandLine 'git', 'tag', '--list'
    }.standardOutput
    return stream.asText.get().split("\n").size()
}

// use git describe to generate a version name looking like
// "<tag>-<number of commits since tag>-<8 char abbreviated commit name>"
def getVersionName = { ->
    def stream = providers.exec {
        commandLine "git", "describe", "--tags", "--long"
    }.standardOutput
    return stream.asText.get().trim() //.replaceAll("[^\\x00-\\x7F]", "")
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
    // jp2-android repo (MIA)
    //maven { url = "https://jcenter.bintray.com/" }
    // 7-Zip-JBinding-4Android 16.02-2.02 repo
    maven { url = 'https://jitpack.io' }
    // commons-compress rc-1 location
    //maven { url "https://repository.apache.org/content/repositories/orgapachecommons-1628/" }
}

android {
    compileSdk = 35
    //compileSdkPreview "VanillaIceCream"
    //buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId = "com.github.edeso.bubble2"
        namespace = "com.nkanaev.comics"

        minSdk = 31
        targetSdk = 35
        //targetSdkPreview "VanillaIceCream"
        multiDexEnabled = true

        versionName = getVersionName()
        versionCode = getVersionCode()
        //setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")
        base.archivesName = "Bubble2-${versionName}"

        manifestPlaceholders = [
                appIcon: "@drawable/icon_squared"
        ]
   }

    // reconfigure to create APK file without '-release' suffix.
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals("release")) {
            variant.outputs.all {
                outputFileName = "${base.archivesName.get()}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            applicationIdSuffix = '.debug'
            versionNameSuffix = '-DEBUG'
            manifestPlaceholders = [
                    appIcon: "@drawable/icon_squared_debug"
            ]
        }
        release {
            // can't use either of the below, f**ks up JP2 support

            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled = false
            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources = false
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
    dependenciesInfo {
        /**
        * BLOB that's supposed to be just a binary representation of your app's 
        * dependency tree. But as it's encrypted with a public key belonging
        * to Google, only Google can read it â€“ and nobody else can even verify
        * what it really contains.
        */
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }

}

dependencies {
    // appcompat v1.7+ API21
    implementation 'androidx.appcompat:appcompat:1.7.0'
    // for multiple dex files on pre-Android 5 devices
    implementation 'androidx.multidex:multidex:2.0.1'
    // startup-runtime 1.2.+ API21
    implementation 'androidx.startup:startup-runtime:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    // recyclerview 1.4.+ API21
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    // fragment v1.7+ API19
    implementation 'androidx.fragment:fragment:1.8.5'
    // material v1.12+ API19
    implementation 'com.google.android.material:material:1.12.0'
    // constraintlayout 2.2.0+ needs API21
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'com.github.junrar:junrar:7.5.5'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'org.apache.commons:commons-compress:1.27.1'
    implementation 'org.tukaani:xz:1.10'

    // 7-Zip-JBinding-4Android 16.02-2.03+ API21
    implementation 'com.github.edeso:7-Zip-JBinding-4Android:2025.01.26-1'

    implementation 'com.github.luben:zstd-jni:1.5.6-9@aar'
    implementation 'org.brotli:dec:0.1.2'
    // jp2-android 1.0.4+ API21, gemalto aar vanished from jcenter
    implementation 'dev.keiji.jp2:jp2-android:1.0.5'

    implementation "androidx.preference:preference:1.2.1"

    implementation 'com.github.awxkee:avif-coder:2.1.4' // or any version above picker from release tags

    // workaround fix 'Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in modules ...'
    //implementation(platform("org.jetbrains.kotlin:kotlin-bom:2.0.0"))

    //implementation fileTree(dir: 'lib', include: ['*.jar','*.aar'])
}

// write deps to BuildConfig for AboutFragment to display
task CaptureDependencies {
    def list = []
    configurations.getByName('implementation').allDependencies.each {
        //println "${it.group} ${it.name} ${it.version}"
        list += "${it}"
    }
    def json= groovy.json.StringEscapeUtils.escapeJavaScript(groovy.json.JsonOutput.toJson(list))
    android.buildTypes.each {
        it.buildConfigField 'String', 'DEPENDENCIES', "\"${json}\""
    }
}