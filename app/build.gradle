apply plugin: 'com.android.application'

def getVersionCode = { ->
    def code = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = code
    }
    return code.toString().split("\n").size()
}


def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine "git", "describe", "--tags", "--long"
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
    // jp2-android repo
    jcenter()
}

android {
    compileSdk 33
    //buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.edeso.bubble2"
        namespace "com.nkanaev.comics"

        minSdkVersion 16
        targetSdkVersion 33
        multiDexEnabled true

        versionName getVersionName()
        versionCode getVersionCode()
        setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/DEPENDENCIES.txt', 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt']
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    // for multiple dex files on pre-Android 5 devices
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.startup:startup-runtime:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.fragment:fragment:1.5.5'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.github.junrar:junrar:7.5.3'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'org.apache.commons:commons-compress:1.22'
    //implementation fileTree(dir: 'lib', include: ['*.jar'])
    implementation 'org.tukaani:xz:1.9'
    implementation 'com.github.luben:zstd-jni:1.5.2-5@aar'
    implementation 'org.brotli:dec:0.1.2'
    implementation 'com.gemalto.jp2:jp2-android:1.0.3'
}
